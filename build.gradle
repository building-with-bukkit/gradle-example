// Hello! This is a comment, generally build files aren't filled with so many comments, but to
// make it easier to understand - this file is full of them!

// The majority of this file is generated by executing gradle init - but we'll walk through each
// line with you. 

// At the top of every gradle.build file we need to specifiy our plugins.  
apply plugin: "java"

// This will set variables across all projects in this gradle project, although there is only one project 
// in our setup - in the future if you create submodules you will want to use this.
allprojects {
	version = "1.0.0-SNAPSHOT" // The version, this will be processed through all resources
	group = "com.buildingwithbukkit.example" // The group, this will be processed through all resources
}


// In this section you declare where to find the dependencies of your project
repositories {
    // By default,there are a few built-in repositories you can grab using a function.
    // In this example, we use 'jcenter' for resolving dependencies. 
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    
    // Now, we also need to specify this maven repository. This repository will link us to
    // depend on the spigot API!
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

// In this section you declare the dependencies for your production and test code
dependencies {
	// There are two main ways to specify a repository that you need to be aware of:
	// - compile: This dependency will be INCLUDED with the jar
	// - compileOnly: This dependency will only be needed for compile, and will
	
	// We only want to use compileOnly for the spigot API.
	compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.12.1-R0.1-SNAPSHOT";	
}

processResources {
	// Pass our project version as a property - it will update in plugin.yml for us
	inputs.property "version", project.version

	// Replace stuff in plugin.yml, nothing else
	// If you wanted to 
	from (sourceSets.main.resources.srcDirs) {
		include "plugin.yml"
		// include "version.yml" // If you wanted to process variables on other files, add them here

		// Replace version
		expand "version": project.version
	}

	// Copy everything else that is not the plugin.yml
	from (sourceSets.main.resources.srcDirs) {
		exclude "plugin.yml"
		// exclude "version.yml" // Exclude other files that you process here
	}
}

// That is it! You can now compile against the Spigot API!
// From terminal/command prompt you can run:
// $ gradle build

// or if you don't have gradle installed execute either:
// $ ./gradlew # linux
// $ ./gradlew.bat # windows

// It will new be in the build/libs directory! 
